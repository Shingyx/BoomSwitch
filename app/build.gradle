apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-android'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
try {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (FileNotFoundException ignored) {
    logger.warn('keystore.properties not found')
}

android {
    namespace 'com.github.shingyx.boomswitch'
    signingConfigs {
        release {
            storeFile rootProject.file(keystoreProperties['storeFile'] ?: "default.jks")
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdk 34
    defaultConfig {
        applicationId "com.github.shingyx.boomswitch"
        minSdk 23
        targetSdk 34
        versionCode 11
        versionName "1.7"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            signingConfig signingConfigs.release
        }
    }
    buildFeatures {
        viewBinding true
    }
    kotlin {
        jvmToolchain(17)
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
}
